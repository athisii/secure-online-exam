plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'

}

group 'com.cdac'
version '1.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.cdac.secureonlineexam'
    mainClass = 'com.cdac.secureonlineexam.App'
}


jar {
    manifest {
        attributes("Main-Class": "com.cdac.secureonlineexam.App")
    }
}

javafx {
    version = '17.0.10'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')

    // reading and writing QR code
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.google.zxing:javase:3.5.3'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}


def jarDir = "/home/athisii/lib"
// custom Task
task copyDependencies(type: Copy) {
    group "copy dependencies"
    description "Copy dependencies to ${jarDir}"
    configurations.runtimeClasspath.files.collect {
        it.getAbsolutePath()
    }.forEach {
        from(it)
        into jarDir
    }
}